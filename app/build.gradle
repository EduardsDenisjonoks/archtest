apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'androidx.navigation.safeargs.kotlin'

static def generateVersionCode() {
    def result = "git rev-list HEAD --count".execute().text.trim()
    if (result.empty) throw new RuntimeException("Could not generate versioncode on this platform? Cmd output: ${result.text}")
    return result.toInteger()
}

static def generateVersionName() {
    def result = "git describe --tag --abbrev=0".execute().text.trim()
    if (result.empty) return "1.0.0"
    return result
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.exail.archtest"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            keyAlias = keystoreProperties['keyAlias']
            keyPassword keystoreProperties["keyPassword"]
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
        }
    }

    dataBinding {
        enabled = true
    }
    androidExtensions {
        experimental = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'
    /*VIEW PAGER 2*/
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    /*IMAGES*/
    implementation 'com.squareup.picasso:picasso:2.71828'
    /*RETROFIT*/
    implementation 'com.squareup.retrofit2:retrofit:2.7.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.7.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.3.1'
    /*KOIN*/
    implementation 'org.koin:koin-android-viewmodel:2.0.1'
    /*COROUTINES*/
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.1.0'
    /*NAVIGATION*/
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.0-rc04'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.0-rc04'
    /*PAGING*/
    implementation 'androidx.paging:paging-runtime-ktx:2.1.1'
    /*TIMBER*/
    implementation 'com.jakewharton.timber:timber:4.7.1'
    /*FIREBASE*/
    implementation 'com.google.firebase:firebase-analytics:17.2.2'
    implementation 'com.google.firebase:firebase-perf:19.0.5'
    /*CRASHLYTICS*/
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
apply plugin: 'com.google.gms.google-services'
